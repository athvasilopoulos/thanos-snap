#version 330 core

// Interpolated values from the vertex shaders
in vec3 vertex_position_modelspace;
in vec3 vertex_position_worldspace;
in vec3 vertex_position_cameraspace;
in vec3 vertex_normal_cameraspace;
in vec2 vertex_UV;

// Uniform variables
uniform mat4 V;
uniform bool balls;
uniform float disp_level;

// Phong
// light properties
struct Light {
    vec4 La;
    vec4 Ld;
    vec4 Ls;
    vec3 lightPosition_worldspace;
    float power;
};
uniform Light light = Light(
    vec4(1, 1, 1, 1),
    vec4(1, 1, 1, 1),
    vec4(1, 1, 1, 1),
    vec3(4, 4, 10),
    20.0f
);

// materials
struct Material {
    vec4 Ka;
    vec4 Kd;
    vec4 Ks;
    float Ns;
};
uniform Material mtl1 = Material(
    vec4(0.24725, 0.1995, 0.0745, 1),
    vec4(0.75164, 0.60648, 0.22648, 1),
    vec4(0.628281, 0.555802, 0.366065, 1),
    51.2f
);
uniform Material mtl2 = Material(
    vec4(0.19225, 0.19225, 0.19225, 1),
    vec4(0.50754, 0.50754, 0.50754, 1),
    vec4(0.508273, 0.508273, 0.508273, 1),
    51.2f
);

// Output data
out vec4 fragmentColor;

void phong();

void main() {
    // Discard the models' fragments if they are bellow
    // the cutoff disp_level
    if(!balls && vertex_position_modelspace.y > disp_level){
        discard;
    }
    // Draw the scene applying the phong lighting model
    phong();
}

void phong() {
    vec4 _Ks, _Kd, _Ka;
    float _Ns;
    if (balls){
        _Ks = mtl1.Ks;
        _Kd = mtl1.Kd;
        _Ka = mtl1.Ka;
        _Ns = mtl1.Ns;
    }
    else {
        _Ks = mtl2.Ks;
        _Kd = mtl2.Kd;
        _Ka = mtl2.Ka;
        _Ns = mtl2.Ns;
    }
    
    // model ambient intensity (Ia)
    vec4 Ia = light.La * _Ka;

    // model diffuse intensity (Id)
    vec3 N = normalize(vertex_normal_cameraspace);
    vec3 L = normalize((V * vec4(light.lightPosition_worldspace, 1)).xyz
        - vertex_position_cameraspace);
    float cosTheta = clamp(dot(L, N), 0, 1);
    vec4 Id = light.Ld * _Kd * cosTheta;

    // model specular intensity (Is)
    vec3 R = reflect(-L, N);
    vec3 E = normalize(- vertex_position_cameraspace);
    float cosAlpha = clamp(dot(E, R), 0, 1);
    float specular_factor = pow(cosAlpha, _Ns);
    vec4 Is = light.Ls * _Ks * specular_factor;

    //model the light distance effect
    float distance = length(light.lightPosition_worldspace
        - vertex_position_worldspace);
    float distance_sq = distance * distance;

    // final fragment color
    fragmentColor = vec4(
        Ia +
        Id * light.power / distance_sq +
        Is * light.power / distance_sq);
}
